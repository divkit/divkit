apply from: "${project.projectDir}/../div-library.gradle"
apply from: "${project.projectDir}/../div-tests.gradle"
apply from: "${project.projectDir}/../publish-android.gradle"

def generatedSrcDir = new File(buildDir, "generated/source/api")
def generatedSrcSharedDataDir = new File(buildDir, "generated/source/data")

allOpen {
    annotation("com.yandex.div.core.annotations.Mockable")
}

android {

    sourceSets {
        main {
            java.srcDirs += generatedSrcDir
            java.srcDirs += generatedSrcSharedDataDir
        }
    }

    libraryVariants.all { variant ->
        variant.preBuildProvider.configure {
            dependsOn "generateDivModel"
            dependsOn "generateDivSharedData"
        }
    }
}

dependencies {
    implementation project(path: ':assertion')
    implementation project(path: ':div-core')
    implementation project(path: ':div-evaluable')
    implementation project(path: ':div-json')
    implementation project(path: ':utils')
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin"
}

task generateDivModel(type: Exec) {
    def execPath = new File(projectDir, '../../../api_generator/api_generator.sh').absolutePath
    def configPath = new File(projectDir, 'div2-generator-config.json').absolutePath
    def schemaPath = new File(projectDir, "../../../schema").absolutePath
    def modelPath = new File(generatedSrcDir, "com/yandex/div2")

    doFirst {
        println "Process schema: $schemaPath"
        println commandLine
    }

    commandLine execPath, configPath, schemaPath, modelPath

    inputs.file configPath
    inputs.dir schemaPath
    outputs.dir modelPath
}

task generateDivSharedData(type: Exec) {
    def execPath = new File(projectDir, '../../../api_generator/api_generator.sh').absolutePath
    def sharedDataConfigPath = new File(projectDir, 'div2-shared-data-generator-config.json').absolutePath
    def sharedDataPath = new File(projectDir, "../../../shared_data").absolutePath
    def modelPath = new File(generatedSrcSharedDataDir, "com/yandex/div2")

    doFirst {
        println "Process shared data: $sharedDataPath"
        println commandLine
    }

    commandLine execPath, sharedDataConfigPath, sharedDataPath, modelPath

    inputs.file sharedDataConfigPath
    inputs.dir sharedDataPath
    outputs.dir modelPath
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
        freeCompilerArgs += '-opt-in=com.yandex.div.data.DivModelInternalApi'
    }
}
